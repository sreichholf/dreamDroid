buildscript {
    repositories {
        mavenCentral()
        jcenter()
        flatDir{
            dirs 'libs'
        }
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.2.1'
    }
    ext.kotlin_version = '1.6.21'
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }

}
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

repositories {
    google()
    jcenter()
    maven { url "https://jitpack.io" }
}

dependencies {
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.legacy:legacy-preference-v14:1.0.0'
    implementation 'androidx.preference:preference:1.2.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.leanback:leanback:1.0.0'
    implementation 'androidx.leanback:leanback-preference:1.0.0'
    implementation 'androidx.annotation:annotation:1.3.0'
    implementation 'androidx.fragment:fragment:1.4.1'
    implementation 'com.google.android.material:material:1.6.0'
    implementation 'org.videolan.android:libvlc-all:3.1.12'
    implementation 'com.simplecityapps:recyclerview-fastscroll:2.0.1'
    implementation 'com.github.StephenVinouze:MaterialNumberPicker:1.0.5'
    implementation 'com.github.chrisbanes:PhotoView:2.3.0'
    implementation 'com.squareup.okhttp3:okhttp:3.12.7'
    implementation 'com.squareup.picasso:picasso:2.8'
    implementation 'commons-net:commons-net:3.6'
    implementation 'commons-io:commons-io:2.6'
    implementation 'org.openhab.jmdns:jmdns:3.4.2'
    implementation 'io.noties.markwon:core:4.6.2'
    implementation project(':libraries:AttributionPresenter:library')
    implementation project(':libraries:MemorizingTrustManager')
    implementation project(':libraries:simple-gauge-android:gaugelibrary')
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'net.sourceforge.streamsupport:android-retrostreams:1.7.1'
    implementation 'com.evernote:android-state:1.4.1'
    annotationProcessor 'com.evernote:android-state-processor:1.4.1'
    implementation 'com.github.livefront:bridge:v2.0.2'
    implementation 'com.jakewharton:butterknife:10.2.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.2.0'
    implementation 'androidx.navigation:navigation-fragment:2.4.2'
    implementation 'androidx.navigation:navigation-ui:2.4.2'
    implementation 'androidx.slidingpanelayout:slidingpanelayout:1.2.0'
}

android {
    compileSdkVersion Integer.parseInt(project.COMPILE_SDK_VERSION)
    buildToolsVersion project.BUILD_TOOLS_VERSION

    defaultConfig {
        versionCode 444
        versionName="1.6.444"
        minSdkVersion 17
        targetSdkVersion 31
        multiDexEnabled true
        sourceSets {
            main {
                manifest.srcFile 'AndroidManifest.xml'
                java.srcDirs = ['src']
                res.srcDirs = ['res']
            }
        }
    }
    flavorDimensions "distribution"
    productFlavors {
        google {
            dimension "distribution"
            applicationId "net.reichholf.dreamdroid"
        }
        fdroid {
            dimension "distribution"
            applicationId "net.reichholf.dreamdroid"
        }
        amazon {
            dimension "distribution"
            applicationId "net.reichholf.dreamdroid.amazon"
        }
    }
    splits {
        abi {
            enable true
            reset()
            include 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a' //select ABIs to build APKs for
            universalApk true //generate an additional APK that contains all the ABIs
        }
    }

    project.ext.versionCodes = ['armeabi': 1, 'armeabi-v7a': 2, 'arm64-v8a': 3, 'mips': 5, 'mips64': 6, 'x86': 8, 'x86_64': 9]

    android.applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.versionCodeOverride =
                    project.ext.versionCodes.get(output.getFilter(
                            com.android.build.OutputFile.ABI), 99) * 10000000 + android.defaultConfig.versionCode
        }
    }

    buildTypes {
        debug {
            buildConfigField "long", "BUILD_TIME", "0L"
            applicationIdSuffix '.debug'
            versionNameSuffix "-debug"
            resValue "string", "app_name", "@string/app_name_debug"
            resValue "string", "app_name_tv", "@string/app_name_tv_debug"
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            debuggable false
            // Add build date object to BuildConfig
            buildConfigField "long", "BUILD_TIME", "" + System.currentTimeMillis() + "L"
            resValue "string", "app_name", "@string/app_name_release"
            resValue "string", "app_name_tv", "@string/app_name_tv_release"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    lint {
        abortOnError false
    }
}
